let minSampleCount   = 1;
let startTime        = ago(14d);
let targetPlayerCount = 1;
let minSessionTime = 60;
let maxSessionTime = 1000.0; // 0 to disable

// Base query to retrieve raw events
let processMetricsQuery =
GameServerLogs
| where TimeGenerated > startTime
| where JsonDetails has "processMetrics"
| project TimeGenerated, Host, JsonDetails, Location, Profile
| sort by TimeGenerated asc
| extend MetricsId   = strcat(TimeGenerated, "_", Host)
| extend d           = parse_json(JsonDetails)
| extend MetricsTime = todouble(d.elapsedTimeSeconds)
| where maxSessionTime != 0.0 and MetricsTime > minSessionTime and MetricsTime < maxSessionTime;

// Expand cpu histogram bucket, one bucket per row
let processQueryCpu =
processMetricsQuery
| mv-expand d.processMetrics.cpuHistogram
| extend b          = parse_json(d_processMetrics_cpuHistogram)
| extend BucketMin  = todouble(b.min)
| extend BucketN    = todouble(b.n)
| extend BucketAvg  = todouble(b.avg)
| project TimeGenerated, MetricsId, BucketMin, BucketN, BucketAvg,
          Host, MetricsTime, Profile;

// Roll-up cpu histogram buckets per session
let avgCpuPerSession =
processQueryCpu
| summarize
      TotalSamples    = sum(BucketN),
      WeightedCpuSum  = sum(BucketAvg * BucketN),
      SessionTimeSecs = max(MetricsTime)
    by MetricsId, Host, TimeGenerated, Profile
| extend AverageCpuPct = WeightedCpuSum / TotalSamples
| project TimeGenerated, MetricsId, AverageCpuPct,
          TotalSamples, SessionTimeSecs, Host, Profile,
          WeightedCpuSum;

// Roll-up sessions by profile
let avgCpuByProfile =
avgCpuPerSession
| summarize
      ProfileWeightedCpuSum = sum(WeightedCpuSum),
      ProfileTotalSamples   = sum(TotalSamples)
    by Profile
| extend AverageCpuPct = ProfileWeightedCpuSum / ProfileTotalSamples
| project Profile, AverageCpuPct, ProfileTotalSamples
| sort by AverageCpuPct desc;

// // Custom: unity 6 cpu comparison
// avgCpuByProfile
// | where Profile in ("dev1", "nightlyrelease", "nightlymain")
// | project Profile, AverageCpuPct        // pick the 2 columns you want to plot
// | order by Profile asc
// | render columnchart
 
// Custom: unity 6 cpu vs elapsed session time
avgCpuPerSession
| where Profile in ("dev1", "nightlyrelease", "nightlymain")
| project SessionTimeSecs, AverageCpuPct, Profile
| summarize AvgCpuPct = avg(AverageCpuPct)    // mean of all sessions in the bin
          by ElapsedBucket = bin(SessionTimeSecs, 60), Profile
| render linechart 