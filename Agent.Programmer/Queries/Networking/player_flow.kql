// ─────────────────────────────────────────────────────────────────────────────
// Parameters – adjust as needed
// ─────────────────────────────────────────────────────────────────────────────
//set query_results_cache_max_age = time(15m);

// ─────────────────────────────────────────────────────────────────────────────
// Server logs for a particular client session
// ─────────────────────────────────────────────────────────────────────────────
let ServerLogsForClientSession = (
        clientSessionId:string,
        lookback:timespan = 30d    // how far back to search
)
{
    let startTime = now() - lookback;

    // All hosts that ever touched that session within the look-back
    let serverHostsInClientSession =
        GameServerLogs
        | where TimeGenerated > startTime
        | where PlayerClientSessionId has clientSessionId
        | distinct Host;


    // All logs from those hosts in that window
    GameServerLogs
    | where TimeGenerated > startTime
    | where Host in (serverHostsInClientSession)
    | project-reorder TimeGenerated, Host, Level, Message, StackTrace, JsonDetails
    | sort by TimeGenerated asc
};


// ─────────────────────────────────────────────────────────────────────────────
// Server logs for a particular host around a particular time
// ─────────────────────────────────────────────────────────────────────────────
let ServerLogsBetweenTime = (
        host:string,                 // e.g. "prod-prod-a-eu-west-787v5-rlx2l"
        startTime:datetime,          // inclusive lower bound
        endTime:datetime             // inclusive upper bound
)
{
    GameServerLogs
    | where Host == host
    | where TimeGenerated between (startTime .. endTime)
    | project-reorder TimeGenerated, Host, Level, Message, StackTrace, JsonDetails
    | sort by TimeGenerated asc
};

let ServerLogsAroundTime = (
        host:string,
        pivot:datetime,
        win:timespan = 10s      // default keeps the call site short
)
{
    GameServerLogs
    | where Host == host
    | where TimeGenerated between (pivot - win .. pivot + win)
    | project-reorder TimeGenerated, Host, Level, Message, StackTrace, JsonDetails
    | sort by TimeGenerated asc
};

let AccountIdByClientSession = (clientSessionId:string, lookback:timespan = 30d)
{
    toscalar(ServerLogsForClientSession(clientSessionId, lookback)
    // keep only rows that actually have a JSON payload
    | where PlayerClientSessionId has clientSessionId
    | where isnotempty(JsonDetails)
    | extend details = todynamic(JsonDetails)
    | extend MasterPlayerAccountId = tostring(details.MasterPlayerAccountId)
    | where MasterPlayerAccountId != ""
    | distinct MasterPlayerAccountId          // dedupe
    | take 1)
};

// ─────────────────────────────────────────────────────────────────────────────
//  All messages in a client-session that reference any of its connection-ids
// ─────────────────────────────────────────────────────────────────────────────
let ConnectionMessagesForClientSession = (
        clientSessionId:string,
        lookback:timespan = 30d
)
{
    // 1. Figure out the master-account for this client-session
    let accountId = AccountIdByClientSession(clientSessionId, lookback);

    // 2. Collect every connection-id that appears in its "LoginWithAuthToken succeeded" lines
    let connectionIds =
        ServerLogsForClientSession(clientSessionId, lookback)
        | where Message contains accountId
        | where Message has "LoginWithAuthToken succeeded"
        | parse Message
                with *
                     "LoginWithAuthToken succeeded: newEndpoint = " ConnectionId:long
                     ", newUserIdentity = "  UserIdentity
        | distinct tostring(ConnectionId)
        | summarize make_list(ConnectionId);     // dynamic array of ids

    // 3. Return *all* log lines that mention ANY of those ids
    ServerLogsForClientSession(clientSessionId, lookback)
    | where Message has_any (connectionIds)
    | project TimeGenerated, Host, Level, Message, StackTrace
    | sort by TimeGenerated asc
};

// ─────────────────────────────────────────────────────────────────────────────
//  Filter a table to Cosmos operations for a given account
// ─────────────────────────────────────────────────────────────────────────────
let CosmosOperations = (
        T:(TimeGenerated:datetime,
           Message:string,
           JsonDetails:dynamic),          // declare only what you need
        targetAccount:string,            // e.g. "9B3EB06BA7F5E067"
        opKind:string = "Update"         // optional: "Update", "Load", etc.
)
{
    T
    | where Message contains "operation completed"
    | where targetAccount != "" and JsonDetails contains targetAccount
    | extend d = todynamic(JsonDetails)
    | extend
        MasterAccountId = tostring(d.MasterPlayerAccountId),
        EntityId        = tostring(d.EntityId),
        SeqNo           = toint(d.Details.seqNo),
        Operation       = tostring(d.Operation),
        Category        = tostring(d.Category)
    //| where Operation has opKind                  // defaults to "Update"
    | where Category !endswith "charactersummary" // trim noisy summaries
    | project-reorder TimeGenerated, MasterAccountId, EntityId,
                      Category, Operation, SeqNo, Message, JsonDetails
};

// ─────────────────────────────────────────────────────────────────────────────


// Generate some logs of interest (disconnects)
//ConnectionMessagesForClientSession("c70d5ce51f964816a92c117068eda5f8", 30d)
//| where Message contains "Disconnected, reason:"
//| where Message contains AccountIdByClientSession("c70d5ce51f964816a92c117068eda5f8", 30d)


//https://app.clickup.com/t/42097836/QA-530965
//ConnectionMessagesForClientSession("184c1c3012cf4a0c8661e8f31ae7a3fb", 30d)
//ServerLogsBetweenTime("prod-prod-a-eu-west-787v5-rlx2l", datetime(2025-05-17 12:36:58.4856260Z), datetime(2025-05-17 12:39:43.2468690Z))
//| where JsonDetails contains "Update"
//| invoke CosmosOperations("E3464F90A60EDEF")      // opKind defaults to "Update"
//| project TimeGenerated, Level, Message
//| where Level has "error"
//| where Message contains "642078835591289303"
//| where Message contains "E3464F90A60EDEF" or JsonDetails contains "E3464F90A60EDEF"
//| where Message contains AccountIdByClientSession("c70d5ce51f964816a92c117068eda5f8", 30d)

//https://app.clickup.com/t/42097836/QA-530627
// ConnectionMessagesForClientSession("65850fc45168490abace3ef0336f4cb7", 30d)
// //| where Message contains "Disconnected, reason:"

//https://app.clickup.com/t/42097836/QA-493603  6f9ad00b619844d79e6ca1003392dabc A0C111E2A8DF75D5
// TimeGenerated	Host	Level	Message
// 2025-04-22 22:31:50.6033390	prod-prod-a-towns-us-east-whfxl-z9zv9	info	NetConnection(5255260002526592121) status changed (status: Disconnected, reason: 'Loading screen took too long')
// 2025-04-22 22:35:17.1593040	prod-prod-a-towns-us-east-nhk9w-942xk	info	NetConnection(-5133869469244528529) status changed (status: Disconnected, reason: 'Loading screen took too long')
// 2025-04-22 22:38:37.1840450	prod-prod-a-towns-us-east-whfxl-z9zv9	info	NetConnection(5255260002526592121) status changed (status: Disconnected, reason: 'Loading screen took too long')
//ConnectionMessagesForClientSession("6f9ad00b619844d79e6ca1003392dabc", 60d)
//| where Message contains "Disconnected, reason:"
//| project-reorder TimeGenerated, Message

//ServerLogsBetweenTime("prod-prod-a-towns-us-east-whfxl-z9zv9", datetime(2025-04-22 22:28:47.2229370Z), datetime(2025-04-22 22:31:50.6709800))
//| where Level has "error"
//| where Message "LoadCharacter"
//| where Message contains "A0C111E2A8DF75D5"
//| invoke CosmosOperations("A0C111E2A8DF75D5")

// ──────────────────────────────────────────────────────────────
//  Connect/disconnect events
// ──────────────────────────────────────────────────────────────
let PlayerConnects = (_from:datetime, _to:datetime)
{
    GameServerLogs
    | where TimeGenerated between (_from .. _to)
    | where Message has "status: Connected, reason"
    | project TimeGenerated
};

let AbnormalDisconnects = (_from:datetime, _to:datetime)
{
    GameServerLogs
    | where TimeGenerated between (_from .. _to)
    | where Message has "Disconnected, reason:"
    | parse Message with * "Disconnected, reason: '" Reason:string "'" *
    | where Reason !contains "shutting down"
    | where Reason !contains "Closing existing"
    | where Reason !contains "Kick"
    
    | project TimeGenerated, Message, Reason
};

let AbnormalDisconnects2 = (_from:datetime, _to:datetime)
{
    GameServerLogs
    // time window
    | where TimeGenerated between (_from .. _to)

    // the old style always looks like:
    //  "SERVER: Found a message of type StatusChanged with contents: Disconnected <reason text>"
    | where Message has              "Found a message of type"
      and  Message has              "Disconnected"

    // pull the text that follows the word "Disconnected" – that is the reason
    | extend Reason = extract(@"Disconnected\s+(.+)$", 1, Message)

    // keep only genuine abnormal cases (same exclusions we use in AbnormalDisconnects)
    | where not (Reason has "Client shutting down"
              or Reason has "Kicked for inactivity"
              or Reason has "Closing existing server connection and creating new connection")

    // columns to return
    | project TimeGenerated, Reason

};
// ──────────────────────────────────────────────────────────────
//  Daily abnormal-disconnect breakdown *by reason*
// ──────────────────────────────────────────────────────────────
let DailyDisconnectReasonBreakdown = (startTime:datetime, endTime:datetime)
{
    //
    // 1. "Connected" events per day
    //
    let ConnectsPerDay =
        PlayerConnects(startTime, endTime)
        | summarize Connects = count()
          by Day = bin(TimeGenerated, 1d);

    //
    // 2. Abnormal disconnects per day, per reason
    //
    let AbnormalPerDay =
        AbnormalDisconnects(startTime, endTime)
        | summarize Disconnects = count()
          by Day = bin(TimeGenerated, 1d);

    //
    // 3. Join & compute rate
    //
    AbnormalPerDay
    | join kind = inner (ConnectsPerDay) on Day
    | extend DisconnectRatePercent =
             100.0 * todouble(Disconnects) / todouble(Connects)
    | project Day,
              Disconnects,
              DisconnectRatePercent
    | order by Day asc, DisconnectRatePercent desc
};


let DisconnectReasonBreakdown = (from:datetime, end:datetime)
{
    // scalar – one number, no join needed
    let ConnectCount =
        toscalar(PlayerConnects(from, end) | count);

    AbnormalDisconnects(from, end)
    | summarize Disconnects = count() by Reason
    | extend DisconnectRatePercent =
         100.0 * todouble(Disconnects) / todouble(ConnectCount)
    | order by DisconnectRatePercent desc
};

//DailyAbnormalDisconnectRate(ago(7d), now())
//DisconnectReasonBreakdown(ago(1d), now())
//DailyDisconnectReasonBreakdown(ago(7d), now())
//DailyAbnormalDisconnectRate(ago(7d), now())
//PlayerConnects(ago(1d), now()) | count
//AbnormalDisconnects(ago(1d), now())

//DisconnectReasonBreakdown(ago(7d), now())
//let start = ago(10d);
//DailyAbnormalDisconnectRate(start, start+3d)
//DisconnectReasonBreakdown(start, start+3d);

//AbnormalDisconnects2(start, start+10m)
//| take 100
// GameServerLogs
// | where TimeGenerated between (start .. start+1d)
//| summarize count() by bin(TimeGenerated, 1d)
 
// //https://app.clickup.com/t/42097836/QA-536408 b3ed12ead448494cb3d60caba50417fc then 38581fb135824f9cb7da4d212baa0c6e
// ConnectionMessagesForClientSession("b3ed12ead448494cb3d60caba50417fc", 7d)
// | extend TimeAgo = now() - TimeGenerated
// | project-reorder TimeGenerated, TimeAgo
// | where Message contains "Disconnected, reason:"
// //| where Message contains "Player connected"
// //| project-reorder TimeGenerated, Message

// //Checking whether town server has "no reesponse" disconnects for other players; it doesn't, just the one player
// GameServerLogs
// | where TimeGenerated > ago(7d)
// //| where Host has "prod-prod-a-towns-us-west-2prvf-bqbkb"
// | where Host has "prod-prod-a-towns-asia-east-7zksv-5pp7k"
// | project-reorder TimeGenerated, Message, StackTrace
// | where Message contains "Disconnected, reason:"// or Message contains_cs "Connected"
// | where Message contains "remote host"
// //| summarize count() by bin(TimeGenerated, 30m)

// https://app.clickup.com/t/42097836/QA-536332 d9f93a06f26e47dfb30856dfaf1f89e2
//ConnectionMessagesForClientSession("d9f93a06f26e47dfb30856dfaf1f89e2", 7d)
//| where Message contains "Disconnected, reason:"

//https://app.clickup.com/t/42097836/QA-536312 e077709d46f342a2a97bf70eb87365af
//ConnectionMessagesForClientSession("e077709d46f342a2a97bf70eb87365af", 7d)
//| where Message contains "Disconnected, reason:"

// // 31CFBB25AC312CE9
// GameServerLogs
// | where TimeGenerated > ago(7d)
// | where Message contains "31CFBB25AC312CE9"
// | project-reorder TimeGenerated, Message, StackTrace
// | sort by TimeGenerated asc

// GameServerLogs
// | where TimeGenerated > ago(2d)
// // | where Message contains "Received signal: '15'."
// // | summarize count() by bin(TimeGenerated, 15m), Region
// // | sort by TimeGenerated asc
// // | render timechart
// | where Host has "prod-prod-a-eu-west-pcxgr-xx7tv"
// | project-reorder TimeGenerated, Message, PlayerCount
// | sort by TimeGenerated desc

// GameServerLogs
// | where TimeGenerated between (ago(1d) .. now())
// //| where Message has "status: Connected, reason"
// | where Message has "status: Disconnected, reason"
// | project TimeGenerated, Message

let loginEvents = GameServerLogs
| where TimeGenerated > ago(1d)
//| where JsonDetails contains "StartPlayerInitialisation"
//| where Message contains "3A372387A7B19511"
//| where Message contains "exp404test1"
| where Message endswith " joined."
| project TimeGenerated, Message, JsonDetails
| sort by TimeGenerated desc
| where Message contains "talun"// or Message contains "greg"
| take 100
;

let sessionMetricsEvents = playfabevents
| where Timestamp > ago(1d)
| where FullName_Name has "lastepoch_session_performance_metrics" 
    or (FullName_Name has "lastepoch_client_log" and EventData contains "frameTimeMetrics")// backwards compat with old event format
| where EventData contains "236C6464D48B5CEA"
//| take 10
;

//sessionMetricsEvents